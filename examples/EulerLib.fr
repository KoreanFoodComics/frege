{--
    Frequently used functions and values used in Project Euler
    http://projecteuler.net/
-}
package examples.EulerLib where

--- Overloaded values and operations on integers
class Natural (Show a, Integral a, Enum a) => a where
    --- product
    prod :: [a] -> a
    prod xs = fold (*) one xs
    
    --- sum
    summe :: [a] -> a
    summe  xs = fold (+) zero xs
    
    --- the list of prime numbers
    primes :: [a]
    primes = fromInt 2 : filter isPrime (iterate (fromInt 2 +) (fromInt 3))
    
    --- predicate to check if a number is prime
    isPrime :: a -> Bool
    
    {-- check if _n_ is a square and if so return 'Right' _k_
        where _k²_ = _n_, otherwise 'Left' _k_ where _k² < n_
    -}
    isSquare :: a -> Either a a 
    
    --- primefactors in descending order, duplicates are retained.
    factors :: a -> [a]
    
    --- primefactors where equal factors are replaced by their product
    factorProducts :: a -> [a]
    
    -- canonicFactors come in the form (n, f) where _n_ is how often _f_ is used
    --- 'divisors' of a number (including 1 and the number itself)
    --- 'properDivisors' are the 'divisors' excluding the number itself
    divisors, properDivisors :: a -> [a]
    
    divisors n = div (abs n) where
        div n
          | n == zero = []
          | n == one  = [n]
          | otherwise = go (succ one) [one,n] where
            go i acc | sqr > n           = acc
                     | sqr == n          = i:acc
                     | n `rem` i == zero = go (succ i) (i : n `quot` i : acc)
                     | otherwise         = go (succ i) acc
                     where sqr = i*i
    properDivisors n = filter (n!=) (divisors n) 
    
    isPrime n = n > fromInt 1 && pred n primes where
        pred n (a:as)
            | a*a > n          = true
            | n `rem`a == zero = false
            | otherwise        = pred n as
        pred n []            = true       -- to avoid a throws clause
    factors n = loop primes n [] where
        loop (a:as) !n !acc
            | a*a > n           = n:acc
            | n `rem` a == zero = loop (a:as) (n `quot` a) (a:acc)
            | otherwise         = loop as n acc
        loop _ _ _              = []            -- avoid throws clause
    factorProducts n = u (factors n) where
        u [] = []
        u as = prod (takeWhile (head as==) as) : u (dropWhile (head as==) as)
    isSquare n 
        | n > fromInt 10 = loop zero (n `quot` fromInt 4) (n `quot` fromInt 2)
        | n == fromInt 10 = Left  (fromInt 3)
        | n == fromInt  9 = Right (fromInt 3)
        | n <= fromInt  8,
          n >= fromInt  5 = Left  (fromInt 2)
        | n == fromInt  4 = Right (fromInt 2)
        | n <= fromInt  3,
          n >= fromInt  2 = Left  one
        | n == one        = Right one
        | n == zero       = Right zero
        | otherwise       = error ("isSquare argument negative " ++ show n)
        where
            loop a b c
               | b2 == n             = Right b
               | a >=  c             = left b
               | a ==  b, b+one == c = Left b
               | b2 >  n             = loop a ((a+b) `quot` fromInt 2) b
               | otherwise           = loop b ((b+c) `quot` fromInt 2) c
               where !b2 = b*b
                     left b | b*b < n = Left b
                            | otherwise = left (b-one)
        



instance Natural Int
instance Natural Long
instance Natural Integer
                  